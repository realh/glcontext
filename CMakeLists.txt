project(glcontext)
cmake_minimum_required(VERSION 2.8)
set(GLCTX_VERSION_MAJOR 1)
set(GLCTX_VERSION_MINOR 0)

set(GLCTX_SOVERSION ${GLCTX_VERSION_MAJOR})
set(GLCTX_VERSION_STRING "${GLCTX_VERSION_MAJOR}.${GLCTX_VERSION_MINOR}")

include(CMakeDependentOption)

option(BUILD_SHARED_LIBS "Build shared libraries" ON)
option(ENABLE_TESTS "Build tests" ON)

if(NOT MSVC)
    add_compile_options(-Wall -Wextra -Wunused -Wwrite-strings)
endif()


# Work out platform/back-end
if(UNIX AND NOT APPLE)
    # Raspberry Pi?
    if(EXISTS "/opt/vc/include/EGL/egl.h")
        set(HAVE_RPI_INC 1)
    else()
        set(HAVE_RPI_INC 0)
    endif()
    if ("${MY_UNAME}" STREQUAL "armv6l GNU/Linux\n" AND HAVE_RPI_INC)
        option(ENABLE_RPI "Build for Raspberry Pi" ON)
    else()
        option(ENABLE_RPI "Build for Raspberry Pi" OFF)
    endif()
else()
    set(ENABLE_RPI OFF)
endif()

CMAKE_DEPENDENT_OPTION(ENABLE_EGL "Use EGL" OFF "NOT ENABLE_RPI" ON)

if(ENABLE_RPI)
    set(GLCTX_ENABLE_RPI 1)
else()
    set(GLCTX_ENABLE_RPI 0)
endif()

if(ENABLE_EGL)
    set(GLCTX_ENABLE_EGL 1)
    set(GLCTX_ENABLE_GLX 0)
    set(GLCTX_ENABLE_WGL 0)
    set(GLCTX_BACKEND_NAME "EGL")
else()
    set(GLCTX_ENABLE_EGL 0)
    if(WIN32)
        set(GLCTX_ENABLE_GLX 0)
        set(GLCTX_ENABLE_WGL 1)
        set(GLCTX_BACKEND_NAME "WGL")
    else()
        set(GLCTX_ENABLE_GLX 1)
        set(GLCTX_ENABLE_WGL 0)
        set(GLCTX_BACKEND_NAME "GLX")
    endif()
endif()

if(WIN32)
    set(GLCTX_MSWIN 1)
    set(GLCTX_X11 0)
else()
    set(GLCTX_MSWIN 0)
    set(GLCTX_X11 1)
endif()


# Find libraries we need
set(CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/CMake/modules)
if(ENABLE_RPI)
    find_package(BcmHost REQUIRED)
    find_package(RPiEGL REQUIRED)
    set(GLCTX_INCLUDES ${EGL_INCLUDE_DIR} ${BCM_HOST_INCLUDE_DIR})
    set(GLCTX_LIBRARIES ${EGL_LIBRARY} ${BCM_HOST_LIBRARY})
elseif(ENABLE_EGL)
    find_package(EGL REQUIRED)
    set(GLCTX_INCLUDES ${EGL_INCLUDE_DIR})
    set(GLCTX_LIBRARIES ${EGL_LIBRARY})
elseif(GLCTX_ENABLE_GLX)
    find_package(X11 REQUIRED)
    find_package(OpenGL REQUIRED)
    set(GLCTX_INCLUDES ${OPENGL_INCLUDE_DIR} ${X11_INCLUDE_DIR})
    set(GLCTX_LIBRARIES ${OPENGL_gl_LIBRARY} ${X11_LIBRARY})
elseif(GLCTX_ENABLE_WGL)
    find_package(OpenGL REQUIRED)
    set(GLCTX_INCLUDES ${OPENGL_INCLUDE_DIR})
    set(GLCTX_LIBRARIES ${OPENGL_gl_LIBRARY})
endif()

include_directories(${GLCTX_INCLUDES}
        ${PROJECT_SOURCE_DIR} ${PROJECT_BINARY_DIR})

configure_file(glctx/glctx-config.h.in glctx/glctx-config.h)

# Choose source and build library
if(ENABLE_EGL)
    set(GLCTX_SRC glctx/glctx-egl.c)
elseif(GLCTX_ENABLE_GLX)
    set(GLCTX_SRC glctx/glctx-glx.c)
elseif(GLCTX_ENABLE_WGL)
    set(GLCTX_SRC glctx/glctx-wgl.c)
endif()
set(GLCTX_SRC ${GLCTX_SRC} glctx/glctx-common.c)
add_library(glctx ${GLCTX_SRC})
if(BUILD_SHARED_LIBS)
    set_target_properties(glctx PROPERTIES VERSION ${GLCTX_VERSION_STRING})
    if (UNIX)
        set_target_properties(glctx PROPERTIES SOVERSION ${GLCTX_SOVERSION})
    endif()
endif()
target_link_libraries(glctx ${GLCTX_LIBRARIES})

if(ENABLE_TESTS)
    find_package(SDL 1.2)
    if(NOT SDL_FOUND)
        message(WARNING "Unable to build tests: SDL 1.2 not found")
    else()
        if(ENABLE_RPI)
            find_package(RPiGLESv2)
        else()
            find_package(OpenGL)
            find_package(GLESv2)
            find_package(GLEW)
        endif()
        if(OPENGL_FOUND AND NOT GLEW_FOUND)
            message(WARNING "Unable to build OpenGL test: GLEW not found")
            set(OPENGL_FOUND 0)
        endif()
        if(NOT OPENGL_FOUND AND NOT GLESV2_FOUND)
            message(WARNING "Unable to build tests: no OpenGL or GLESv2")
        else()
            if(OPENGL_FOUND)
                message("Building OpenGL test")
            else()
                message("Not building OpenGL test")
            endif()
            if(GLESV2_FOUND)
                message("Building OpenGL ES test")
            else()
                message("Not building OpenGL ES test")
            endif()
        endif()
        if(OPENGL_FOUND)
            add_executable(glctx-testgl tests/glctx-test.c)
            target_compile_definitions(glctx-testgl PUBLIC
                    -DMY_GLCTX_CTX_PROFILE=GLCTX_CTX_PROFILE_OPENGL
                    -DENABLE_OPENGL)
            target_include_directories(glctx-testgl PUBLIC
                    ${GLEW_INCLUDE_DIRS} ${OPENGL_INCLUDE_DIR})
            target_link_libraries(glctx-testgl glctx ${GLCTX_LIBRARIES}
                    ${SDL_LIBRARY} ${GLEW_LIBRARIES} ${OPENGL_gl_LIBRARY})
        endif()
        if(GLESV2_FOUND)
            add_executable(glctx-testgles tests/glctx-test.c)
            target_compile_definitions(glctx-testgles PUBLIC
                    -DMY_GLCTX_CTX_PROFILE=GLCTX_CTX_PROFILE_OPENGLES
                    -DENABLE_OPENGLES)
            target_include_directories(glctx-testgles PUBLIC
                    ${GLESv2_INCLUDE_DIR})
            target_link_libraries(glctx-testgles glctx ${GLCTX_LIBRARIES}
                    ${SDL_LIBRARY} ${GLESv2_LIBRARY})
        endif()
    endif()
endif()
